apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {

    compile 'junit:junit:4.11',
            'org.hamcrest:hamcrest-junit:2.0.0.0',
            'com.fasterxml.jackson.core:jackson-databind:2.8.5',
            'org.apache.httpcomponents:httpclient:4.5.1',
            'org.yaml:snakeyaml:1.17'

    testCompile 'org.mockito:mockito-all:1.9.5'
}

task buildContainer(type:Exec) {
    workingDir './src/docker/upstreamBounce/'
    commandLine 'docker', 'build', '-t', 'krogersnowglobe/upstream-bounce-app:latest', '.'
}


test {
    testLogging {
        showStandardStreams = false
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration/java')
        }
        resources.srcDir file('src/integration/resources')
    }
}
configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging {
        showStandardStreams = true
    }
    maxParallelForks = 10
}

integrationTest.dependsOn buildContainer
integrationTest.mustRunAfter test

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

sourceCompatibility = 1.8
archivesBaseName = "snow-globe"
group "com.kroger.oss.snowGlobe"
version getVersionName()

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

idea {
    project {
        languageLevel = '1.8'
        vcs = 'Git'

        ipr.withXml { xmlFile ->
            // setup Git root
            xmlFile.asNode().component.find { it.@name == 'VcsDirectoryMappings' }.replaceNode {
                component(name: 'VcsDirectoryMappings') {
                    mapping(directory: "", vcs: "")
                    mapping(directory: "\$PROJECT_DIR\$", vcs: 'Git')
                }
            }
        }
    }
}